plugins {
    id 'org.springframework.boot' version '2.2.7.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
}

allprojects {
    group = 'org.vdragun.tms'
    version = '1.0'

    ext {
        // UI
        webjarsLocatorVersion = '0.40'
        webjarsMomentjsVersion = '2.24.0'
        webjarsBootstrapVersion = '4.4.1-1'
        webjarsJQueryVersion = '3.5.1'
        webjarsPopperJsVersion = '2.0.2'
        webjarsFontAwesomeVersion = '5.13.0'
        springDocVersion = '1.3.9'

        // Misc
        swaggerCoreVersion = '2.1.2'

        // Security
        jjwtVersion = '0.9.1'

        // General
        mapStructVersion = '1.3.1.Final'

        // Test
        springMvcTestUtilsVersion = '2.2.1'
        riderJUnitVersion = '1.13.1'
        dbUnitVersion = '2.7.0'
        selenideVersion = '5.12.2'
        jBehaveVersion = '4.6.3'
        jUnitPionerVersion = '0.5.3'
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'checkstyle'

    if (!name.endsWith('application')) {
        bootJar {
            enabled = false
        }

        jar {
            enabled = true
        }
    }

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
            exclude group: 'junit', module: 'junit'
        }
    }

    sourceCompatibility = '1.8'

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    checkstyle {
        configFile file("../checkstyle-config/checkstyle.xml")
        toolVersion = "8.10.1"
        showViolations = true
        ignoreFailures = false
    }

    checkstyleMain {
        source = 'src/main/java'
    }

    checkstyleTest {
        source = 'src/test/java'
    }

    test.mustRunAfter checkstyleMain, checkstyleTest
}